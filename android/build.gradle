group 'ly.img.flutter.imgly_sdk'
version '1.0-SNAPSHOT'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'ly.img.android.sdk'
apply plugin: "com.google.devtools.ksp"

IMGLY.configure {
    pesdk {
        enabled true
    }

    useInheritanceDependencies true

    modules {
        include 'ui:core'
        include 'backend:config-loader'
    }
}

def MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION = "10.9.0"

task checkVersion {
    if (imglyConfig.convertToVersionNumber(imglyConfig.getVersion()) < imglyConfig.convertToVersionNumber(MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION)) {
        String WRONG_VERSION = "" +
                "┌───────────────────────────────────────────────────────────────────────┐\n"+
                "│ PhotoEditor SDK update required!                                      │\n"+
                "│ Please update the plugin version in your 'android/build.gradle' file. │\n"+
                "│ Minimum required dependency: 'ly.img.android.sdk:plugin:${MIN_LY_IMG_ANDROID_SDK_PLUGIN_VERSION}'       │\n"+
                "│ Your are currently using:    'ly.img.android.sdk:plugin:${imglyConfig.getVersion()}'       │\n"+
                "└───────────────────────────────────────────────────────────────────────┘\n"

        throw new GradleScriptException(WRONG_VERSION, new IllegalArgumentException(WRONG_VERSION))
    }
}

preBuild.dependsOn checkVersion

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'ly.img.flutter.imgly_sdk'
    }
    compileSdkVersion 36

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    
    ksp {
        arg("jvmTarget", "17")
    }
    
    // JVM Toolchain configuration to ensure consistent JVM target
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compileOnly "ly.img.android.sdk:serializer:$imglyConfig.version"
}
